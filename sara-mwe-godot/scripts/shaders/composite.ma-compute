#kernel composite

// The master canvas (serves as both background input and final output)
layout( rgba32f, set = 0, binding = 0 ) uniform image2D master_canvas;
// The current layer we are blending on top
layout( rgba32f, set = 0, binding = 1 ) uniform image2D layer_texture;

layout( push_constant, std430 ) uniform PushConstants
{
    float fg_opacity;
}
pc;

[numthreads( 8, 8, 1 )] void composite() {
    ivec2 pixel_coords = ivec2( gl_GlobalInvocationID.xy );
    ivec2 size = imageSize( master_canvas );

    if ( pixel_coords.x >= size.x || pixel_coords.y >= size.y )
    {
        return;
    }

    vec4 bg = imageLoad( master_canvas, pixel_coords );
    vec4 fg = imageLoad( layer_texture, pixel_coords );

    // Apply the layer's opacity to the foreground alpha
    float src_a = fg.a * pc.fg_opacity;
    float dst_a = bg.a;

    // Standard Porter-Duff 'Over' blending
    float out_a = src_a + dst_a * ( 1.0 - src_a );
    vec3 out_rgb = vec3( 0.0 );

    if ( out_a > 0.0 )
    {
        out_rgb = ( fg.rgb * src_a + bg.rgb * dst_a * ( 1.0 - src_a ) ) / out_a;
    }

    imageStore( master_canvas, pixel_coords, vec4( out_rgb, out_a ) );
}
